apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "io.codearte.nexus-staging"

project.getGradle().getTaskGraph().whenReady {
    tasks.withType(GenerateMavenPom.class) {
        destination = file("$projectDir/pom.xml")
    }
}

publishing {
    test

    repositories {
        maven {
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url = snapshot ? snapshotsRepoUrl : releasesRepoUrl
            name = "Sonatype"

            println "Publishing to $name [$url]..."

            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'kalutils'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "KaleidoxUtils"
                description = "Utilities for Kaleidox projects"
                inceptionYear = '2018'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = "burdoto"
                        name = "Tobias Burdow"
                        email = "burdoto@outlook.com"
                    }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    //noinspection GroovyAssignabilityCheck -> false negative
    sign publishing.publications.mavenJava
}

// remove superfluous tasks from NexusStagingPlugin
tasks.removeAll([promoteRepository, closeAndPromoteRepository, getStagingProfile])

nexusStaging {
    username sonatypeUsername
    password sonatypePassword
}
