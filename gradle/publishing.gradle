apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "io.codearte.nexus-staging"

publishing {
    repositories {
        maven {
            url = snapshot
                    ? "https://oss.sonatype.org/content/repositories/snapshots"
                    : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            name = "Sonatype"

            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'kalutils'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "KaleidoxUtils"
                description = "Utilities for Kaleidox projects"
                inceptionYear = '2018'
                url = 'https://github.com/burdoto/KalUtils'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = "burdoto"
                        name = "Tobias Burdow"
                        email = "burdoto@outlook.com"
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/burdoto/KalUtils.git'
                    developerConnection = 'scm:git:ssh://github.com/burdoto/KalUtils.git'
                    url = 'https://github.com/burdoto/KalUtils'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

tasks.removeAll([promoteRepository, closeAndPromoteRepository, getStagingProfile])

nexusStaging {
    username sonatypeUsername
    password sonatypePassword
}
